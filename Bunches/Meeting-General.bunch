---
title: ðŸ“† Meeting - General
---

snippets_path="~/Documents/Automations/Snippets"
applescript_path="~/Documents/Automations/AppleScripts"
shell_path="~/Documents/Automations/ShellCommands"
applications_path="/Applications"

if MeetInOne is not running
<<#Setup Meeting
end

<<#Open Meeting

    <<#On Finish ~1

!<<#End Meeting


___
###[Setup Meeting]

* ${applescript_path}/CloseAppsMeetings.scpt
(pause 1)
@@
(pause 1)

// Open Apps
%Google Chrome
%Bear
%Spotify
%Slack
!Notion
!MeetInOne


//Forces Open Felix chrome profile
* ${applescript_path}/OpenChromeProfile.scpt
//Focuses Apps via shell, because Bunch is fiddly with it.
$ ${shell_path}/OpenMeetingApps.sh

###[Open Meeting]

//Command to Open next Meeting
{$@o}
* ${applescript_path}/PauseSpotify.scpt
* ${applescript_path}/CameraOn.scpt

//Wait Until things have finished launching
###[On Finish]
<${snippets_path}/moom.snippet ~1


// END MEETING
###[End Meeting]
* "${applescript_path}/MergeChromeTabs.scpt"
* tell application "Moom" to arrange windows according to snapshot named "Meeting End"
* "${applescript_path}/CameraOff.scpt"



